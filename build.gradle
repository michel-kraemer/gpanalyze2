buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.alchim31:livereload-jvm:0.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

import java.nio.file.FileSystems
import net_alchim31_livereload.LRServer

mainClassName = 'io.vertx.core.Launcher'

repositories {
    jcenter()
    maven {
        url 'http://download.osgeo.org/webdav/geotools/'
    }
}

dependencies {
    compile "io.vertx:vertx-core:3.4.2"
    compile "io.vertx:vertx-mongo-client:3.4.2"
    compile "io.vertx:vertx-rx-java:3.4.2"
    compile "io.vertx:vertx-web:3.4.2"

    compile "org.webjars:angularjs:1.3.15"
    compile "org.webjars:angular-material:0.10.0"
    compile "org.webjars:jquery:2.1.4"
    compile "org.webjars:leaflet:0.7.3"
    compile "org.webjars.bower:d3:3.5.6"
    compile "org.webjars.bower:mdi:1.1.34"
    compile "org.webjars.bower:ng-file-upload:5.0.9"
    compile "org.webjars.bower:sockjs-client:0.3.4"
    compile "org.webjars.bower:xdate:0.8.1"
    compile "org.webjars.npm:vertx3-eventbus-client:3.4.2"
    
    compile "com.google.maps:google-maps-services:0.2.2"
    compile "org.apache.commons:commons-jcs-core:2.2"
    compile "org.apache.commons:commons-lang3:3.6"
    compile "org.apache.commons:commons-math3:3.6.1"
    compile "commons-io:commons-io:2.5"
    
    compile("org.geotools:gt-geometry:17.2") {
        // exclude invalid dependency
        exclude group: 'javax.media', module: 'jai_core'
    }
    // replace invalid jai_core dependency by a valid one
    compile 'javax.media.jai:com.springsource.javax.media.jai.core:1.1.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

run {
    args = ['run', 'de.undercouch.gpanalyze2.Main', "--redeploy=src/**/*",
        "--launcher-class=$mainClassName", "--on-redeploy=./gradlew build"]
}

task runLiveReloadServer {
    doLast {
        new LRServer(35729, FileSystems.getDefault().getPath("build/resources/main/webroot")).start();
    }
}

run.dependsOn(runLiveReloadServer)
